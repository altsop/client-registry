plugins {
    id "java"
    id "idea"
    id "io.freefair.lombok" version "5.3.0"
    id "org.springframework.boot" version "2.7.2"
    id "io.spring.dependency-management" version "1.0.12.RELEASE"
}

archivesBaseName = 'client-registry'
version = '1.0'
group = 'ee.cloudmore'

ext {
    openApiVersion = "1.6.9"
}

dependencies {

    implementation(
            project(":db"),
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-validation",
            "org.springframework.boot:spring-boot-starter-data-jpa",
            "org.springdoc:springdoc-openapi-ui:$openApiVersion",

            "org.springframework.kafka:spring-kafka",

            "mysql:mysql-connector-java",
            "org.liquibase:liquibase-core"
    )

    annotationProcessor(
            "org.springframework.boot:spring-boot-configuration-processor"
    )

    compileOnly(
            "org.springframework.boot:spring-boot-configuration-processor"
    )

    testImplementation(
            "org.springframework.boot:spring-boot-starter-test",
            "org.testcontainers:mysql:1.17.3",
            "org.testcontainers:kafka:1.17.3",
            "io.rest-assured:rest-assured",
            "org.awaitility:awaitility",
            "org.testcontainers:junit-jupiter:1.17.3"
    )

}

test {
    useJUnitPlatform() {
        excludeTags 'integration-test', 'e2e-test'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task integrationTest(type: Test) {
    group = 'verification'
    description = "Run slower integration tests"
    useJUnitPlatform {
        includeTags 'integration-test'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task e2eTest(type: Test) {
    group = 'verification'
    description = "Run the slowest e2e test"
    useJUnitPlatform {
        includeTags 'e2e-test'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}
